plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'fr.univamu'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.2'
}

sourceCompatibility = "20"
targetCompatibility = "20"


tasks.withType(JavaCompile).all {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['--enable-preview']
}
tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

application {
    mainModule = 'fr.univamu.randomtree'
    mainClass = 'fr.univamu.visualizer.RandomTreeApplication'
}

task runMain(type: JavaExec) {
    group = "application"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'fr.univamu.graph.Main'
}

javafx {
    version = '20.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}